buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
    id "com.iadams.sonar-packaging" version "0.1.3"
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: "com.iadams.sonar-packaging"

group = 'org.sonarsource.github'
version = '1.2.mio'

repositories {
    mavenCentral()
}

dependencies {
    provided "org.codehaus.sonar:sonar-plugin-api:$SONAR_VERSION"
    provided "org.slf4j:slf4j-api:1.7.12"
    provided "com.google.code.findbugs:jsr305:2.0.3"
    compile "commons-io:commons-io:2.4"
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile "org.kohsuke:github-api:1.69"
}

configurations {
    compile.exclude group: "commons-logging"
    compile.exclude group: "org.jenkins-ci"
}

sonarPackaging {
    pluginClass = 'org.sonar.plugins.github.GitHubPlugin'
    pluginName = 'GitHub'
    pluginKey = "github"
    pluginDescription = 'Provide some integration between GitHub and SonarQube'
}

artifacts {
    archives pluginPackaging
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'sonar-github-plugin'

            artifact pluginPackaging

            pom {
                packaging = 'sonar-plugin'
                withXml {
                    def root = asNode()
                    root.appendNode('name', 'SonarQube :: GitHub Plugin')
                    root.appendNode('description', 'Provide some integration between GitHub and SonarQube')
                    root.appendNode('url', 'https://redirect.sonarsource.com/plugins/github.html')
                    root.appendNode('inceptionYear', '2015')

                    def scm = root.appendNode('scm')
                    scm.appendNode('url', 'https://github.com/Miovision/sonar-github')
                    scm.appendNode('connection', 'scm:https://github.com/Miovision/sonar-github.git')
                    scm.appendNode('developerConnection', 'scm:git://github.com/Miovision/sonar-github.git')

                    def license = root.appendNode('licenses').appendNode('license')
                    license.appendNode('name', 'GNU LGPL 3')
                    license.appendNode('url', 'http://www.gnu.org/licenses/lgpl.txt')
                    license.appendNode('distribution', 'repo')

                    def developers = root.appendNode('developers')
                    def henryju = developers.appendNode('developer')
                    henryju.appendNode('id', 'henryju')
                    henryju.appendNode('name', 'Julien Henry')

                    def akowpak = developers.appendNode('developer')
                    akowpak.appendNode('id', 'akowpak-miovision')
                    akowpak.appendNode('name', 'Andrew Kowpak')
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
    key =  project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : null
    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = 'sonar-github'
        desc = 'Provide some integration between GitHub and SonarQube.'
        userOrg = project.hasProperty('bintrayOrg') ? project.getProperty('bintrayOrg') : null
        licenses = ['LGPL-3.0']
        vcsUrl = "https://github.com/Miovision/sonar-github.git"
        websiteUrl = "https://github.com/Miovision/sonar-github"
        issueTrackerUrl = "https://github.com/Miovision/sonar-github/issues"
        labels = ['sonar', 'github']
        publicDownloadNumbers = true

        version {
            name = "$project.version"
            vcsTag = "$project.version"
        }
    }
}